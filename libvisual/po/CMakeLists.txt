PROJECT(po)

SET(DOMAIN ${PACKAGE})
SET(COPYRIGHT_HOLDER ${PACKAGE_VENDOR})
SET(MSGID_BUGS_ADDRESS ${PACKAGE_BUGREPORT})
SET(XGETTEXT_OPTIONS --keyword=_ --keyword=N_)

SET(LINGUAS "")

FIND_PROGRAM(INTLTOOL_UPDATE intltool-update)
MARK_AS_ADVANCED(INTLTOOL_UPDATE)

FIND_PROGRAM(XGETTEXT xgettext)
MARK_AS_ADVANCED(XGETTEXT)

SET(LANGUAGE_TARGETS)

ADD_CUSTOM_TARGET(nls-update-sources
  ${INTLTOOL_UPDATE} -m
  WORKING_DIRECTORY ${po_SOURCE_DIR}
)

ADD_CUSTOM_COMMAND(OUTPUT ${po_BINARY_DIR}/${DOMAIN}.pot
  COMMAND ${XGETTEXT}
  ARGS --files-from ${po_SOURCE_DIR}/POTFILES.in
       -o ${po_BINARY_DIR}/${DOMAIN}.pot
       --package-name=${PACKAGE}
       --package-version=${PACKAGE_VERSION}
       --copyright-holder=${COPYRIGHT_HOLDER}
       --msgid-bugs-address=${MSGID_BUGS_ADDRESS}
       ${XGETTEXT_OPTIONS}
  DEPENDS ${po_SOURCE_DIR}/POTFILES.in
  WORKING_DIRECTORY ${libvisual_SOURCE_DIR}
)

FOREACH(LANGUAGE ${LINGUAS})

  ADD_CUSTOM_TARGET(nls-update-${LANGUAGE}.po
    COMMAND ${GETTEXT_MSGMERGE_EXECUTABLE}
    ARGS -o ${LANGUAGE}.po ${LANGUAGE}.po ${po_BINARY_DIR}/${DOMAIN}.pot
    DEPENDS ${po_SOURCE_DIR}/${LANGUAGE}.po
    WORKING_DIRECTORY ${po_SOURCE_DIR}
  )

  FILE(MAKE_DIRECTORY ${po_BINARY_DIR}/${LANGUAGE}/LC_MESSAGES)

  ADD_CUSTOM_COMMAND(OUTPUT ${po_BINARY_DIR}/${LANGUAGE}/LC_MESSAGES/${DOMAIN}.mo
    COMMAND ${GETTEXT_MSGFMT_EXECUTABLE}
    ARGS -c -o ${po_BINARY_DIR}/${LANGUAGE}/LC_MESSAGES/${DOMAIN}.mo ${po_SOURCE_DIR}/${LANGUAGE}.po
    DEPENDS ${po_SOURCE_DIR}/${LANGUAGE}.po
  )

  INSTALL(FILES ${po_BINARY_DIR}/${LANGUAGE}/LC_MESSAGES/${DOMAIN}.mo
    DESTINATION ${CMAKE_INSTALL_LOCALEDIR}/${LANGUAGE}/LC_MESSAGES
  )

  SET(LANGUAGE_TARGETS ${LANGUAGE_TARGETS} ${po_BINARY_DIR}/${LANGUAGE}/LC_MESSAGES/${DOMAIN}.mo)

ENDFOREACH(LANGUAGE ${LINGUAS})

ADD_CUSTOM_TARGET(nls ALL
  DEPENDS ${po_BINARY_DIR}/${DOMAIN}.pot ${LANGUAGE_TARGETS}
)
