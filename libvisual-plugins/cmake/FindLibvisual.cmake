IF((Libvisual_FIND_VERSION_MAJOR EQUAL 0) AND (Libvisual_FIND_VERSION_MINOR EQUAL 5))
  SET(LIBVISUAL_SUFFIX 0.5)
ENDIF()

SET(LIBVISUAL_PKG "libvisual-${LIBVISUAL_SUFFIX}")

FIND_PACKAGE(PkgConfig REQUIRED)
PKG_CHECK_MODULES(LIBVISUAL ${LIBVISUAL_PKG}>=${Libvisual_FIND_VERSION} IMPORTED_TARGET)

INCLUDE(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(Libvisual DEFAULT_MSG LIBVISUAL_LIBRARIES LIBVISUAL_INCLUDE_DIRS)

IF(LIBVISUAL_FOUND)
  INCLUDE(PkgGetModuleVar)

  # Should we define the imported target ourselves?
  ADD_LIBRARY(Libvisual::Libvisual ALIAS PkgConfig::LIBVISUAL)

  PKG_GET_MODULE_VAR("${LIBVISUAL_PKG}" pluginsbasedir _LV_PKG_VAR_VALUE)
  SET(LV_PLUGIN_DIR "${_LV_PKG_VAR_VALUE}" CACHE PATH "Plugin installation directory")

  PKG_GET_MODULE_VAR("${LIBVISUAL_PKG}" datadir _LV_PKG_VAR_VALUE)
  SET(LV_PLUGIN_DATA_DIR "${_LV_PKG_VAR_VALUE}" CACHE PATH "Plugin data directory")

  PKG_GET_MODULE_VAR("${LIBVISUAL_PKG}" localedir _LV_PKG_VAR_VALUE)
  SET(LV_PLUGIN_LOCALE_DIR "${_LV_PKG_VAR_VALUE}" CACHE PATH "Plugin translations directory")

  SET(LV_ACTOR_PLUGIN_DIR "${LV_PLUGIN_DIR}/actor")
  SET(LV_INPUT_PLUGIN_DIR "${LV_PLUGIN_DIR}/input")
  SET(LV_MORPH_PLUGIN_DIR "${LV_PLUGIN_DIR}/morph")

  MESSAGE(STATUS "Plugins will be installed into ${LV_PLUGIN_DIR}")
  MESSAGE(STATUS "Plugin data will be installed into ${LV_PLUGIN_DATA_DIR}")

  UNSET(_LV_PKG_VAR_VALUE)
ENDIF()

MARK_AS_ADVANCED(LV_PLUGIN_DIR)
MARK_AS_ADVANCED(LV_PLUGIN_DATA_DIR)
MARK_AS_ADVANCED(LV_PLUGIN_LOCALE_DIR)
MARK_AS_ADVANCED(LV_ACTOR_PLUGIN_DIR)
MARK_AS_ADVANCED(LV_INPUT_PLUGIN_DIR)
MARK_AS_ADVANCED(LV_MORPH_PLUGIN_DIR)
